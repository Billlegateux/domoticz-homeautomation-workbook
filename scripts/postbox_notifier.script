! Function: Postbox Notifier
! 20191110 by rwbl
string sFunction = "Postbox Notifier";
string sDate = system.Date("%d.%m"); ! sDate = "09.08"; 
! string sDate = system.Date("%d.%m.%Y"); ! sDate = "09.08.2019"; 
string sTime = system.Date("%H:%M"); ! sTime = "07:32"; 
! string sTime = system.Date("%H:%M:%S"); ! sTime = "07:32:00"; 

! A domoticz alert sensor is used
string nIdx = 189; ! Type: Light/Switch; SubType: On/Off
string sOn = "On";  ! Red
string sText = "Postbox%20opened%20(" # sDate # "%20" # sTime # ")";

! Build the domoticz http rest request url
string cAmp = "&";
string sDomUrl = "'http://domoticz-ip:8080/json.htm"; ! Production
string sUrl = sDomUrl#"?type=command"#cAmp#"param=switchlight"#cAmp#"idx="#nIdx#cAmp#"switchcmd="#sOn#"'";
WriteLine(sUrl);

! OPTION: Run the command without a return result
! res = dom.GetObject("CUxD.CUX2801001:1.CMD_EXEC").State("wget -q -O - "#sUrl);

! OPTION: Run the command with a return result
! Define the command to execute
dom.GetObject("CUxD.CUX2801001:1.CMD_SETS").State ("wget -q -O - "#sUrl);

! Set the return flag to 1 to be able to read the json result
dom.GetObject("CUxD.CUX2801001:1.CMD_QUERY_RET").State (1);

! Start the command, wait till completed and get the result JSON string, i.e.
! {"status" : "OK","title" : "Update Device"}
! NOTE: The script running on the CCU waits until the completion - ensure not to execute commands which take long time.
string sRes = dom.GetObject("CUxD.CUX2801001:1.CMD_RETS").State();
! WriteLine("VT="#sRes.VarType()#"/"#sRes); ! VT=4, {"status" : "OK","title" : "Update Device"}

! handle result
var dl = dom.GetObject ("DomoticzLog");
! Update the var with result text
if (sRes.Find("OK") > 0) {
  dl.Variable(sFunction#"-Last update OK:"#sDate#" "#sTime )
}
else {
  dl.Variable(sFunction#"Last update ERROR:"#sDate#" "#sTime)
};
! WriteLine("VT="#dl.VarType());  ! VT=9
! WriteLine(dl.Variable());       ! shows the last update string
